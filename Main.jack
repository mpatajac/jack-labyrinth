/** Driver code */
class Main {
	function void main() {
		var int size;
		var Maze m;

		var Player p;
		var char move;

		var int seed, lastCellIndex;
		var char beginGame;
		var boolean gameHasEnded;

		// set game state
		let seed = 0;
		let beginGame = 0;
		let gameHasEnded = false;

		// TODO - set it according to user input
		let size = 35;

		// wait for input -> used to determine RNG seed
		do Output.printString("Press any key to start: ");
		while (beginGame = 0) {
			let beginGame = Keyboard.keyPressed();
			let seed = seed + 1;
		}

		// clear screen and draw the grid
		do Screen.clearScreen();
		do Output.moveCursor(0, 0);
		let m = Maze.new(seed, size);

		let lastCellIndex = (m.getRows() * m.getCols()) - 1;

		// create player and start the game
		let p = Player.new(m.getStart(), size, m.getGrid());
		do p.draw();

		while (~(gameHasEnded)) {
			let move = Keyboard.keyPressed();
			if (move > 0) {
				do p.move(move);
				do Sys.wait(200);

				let gameHasEnded = Main.checkForEnd(p, m, lastCellIndex);
			}
		}

		return;
	}

	/** Checks if the game has ended */
	function boolean checkForEnd(Player p, Maze m, int lastCellIndex) {
		var int gridIndex;

		// players current position in the grid
		let gridIndex = (p.j() * m.getCols()) + p.i();
		if (gridIndex = lastCellIndex) {
			do Output.printString("You completed the maze!");
			return true;
		}

		return false;
	}
}