/** Driver code */
class Main {
	function void main() {
		var int size;
		var Maze maze;

		var Player player;
		var char move;

		var int seed, lastCellIndex, moveCount;
		var char difficulty;
		var boolean gameHasEnded;

		// set game state
		let seed = 0;
		let difficulty = 0;
		let moveCount = 0;
		let gameHasEnded = false;

		// draw home screen
		do Main.home();

		// wait for input -> used to determine RNG seed
		while (difficulty = 0) {
			let difficulty = Keyboard.keyPressed();
			let seed = seed + 1;
		}

		// "Loading screen" (for harder mazes)
		do Main.load();

		// set size according to difficulty
		let size = Main.setDifficulty(difficulty);

		// generate and draw the maze
		let maze = Maze.new(seed, size);

		let lastCellIndex = (maze.getRows() * maze.getCols()) - 1;

		// create player and start the game
		let player = Player.new(maze.getStart(), size, maze.getGrid());
		do player.draw();

		while (~(gameHasEnded)) {
			let move = Keyboard.keyPressed();
			if (move > 0) {
				let moveCount = moveCount + 1;

				do player.move(move);
				do Sys.wait(200);

				let gameHasEnded = Main.checkForEnd(
					player, maze, lastCellIndex, moveCount
				);
			}
		}

		return;
	}

	/** Checks if the game has ended */
	function boolean checkForEnd(
		Player player,
		Maze maze,
		int lastCellIndex,
		int moveCount
	) {
		var int gridIndex;

		// players current position in the grid
		let gridIndex = (player.j() * maze.getCols()) + player.i();
		if (gridIndex = lastCellIndex) {
			do Output.printString("You completed the maze!");
			do Output.println();
			do Output.printString("It took you ");
			do Output.printInt(moveCount);
			do Output.printString(" moves.");
			return true;
		}

		return false;
	}

	/** Set cell size according to difficulty */
	function int setDifficulty(int difficulty) {
		// hard (ascii(3) => 51)
		if (difficulty = 51) {
			return 13;
		}

		// medium (ascii(2) => 50)
		if (difficulty = 50) {
			return 24;
		}

		// easy (or invalid input)
		return 35;
	}

	/** Draw home screen */
	function void home() {
		do Output.moveCursor(6, 17);
		do Output.printString("Welcome to Jack Labyrinth!");
		do Output.moveCursor(10, 20);
		do Output.printString("Choose difficulty: ");
		do Output.moveCursor(12, 22);
		do Output.printString("  1 - easy");
		do Output.moveCursor(14, 22);
		do Output.printString("  2 - medium");
		do Output.moveCursor(16, 22);
		do Output.printString("  3 - hard");
		return;
	}

	/** Draw loading scereen */
	function void load() {
		do Screen.clearScreen();
		do Output.moveCursor(10, 15);
		do Output.printString("Generating maze, please wait ...");
		do Sys.wait(300);
		return;
	}
}