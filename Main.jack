/** Driver code */
class Main {
	function void main() {
		var int size;
		var Maze maze;

		var Player player;
		var char move;

		var int seed, lastCellIndex;
		var char beginGame;
		var boolean gameHasEnded;

		// set game state
		let seed = 0;
		let beginGame = 0;
		let gameHasEnded = false;

		// TODO - set it according to user input
		let size = 35;

		// wait for input -> used to determine RNG seed
		do Output.printString("Press any key to start: ");
		while (beginGame = 0) {
			let beginGame = Keyboard.keyPressed();
			let seed = seed + 1;
		}

		// clear screen and draw the grid
		do Screen.clearScreen();
		do Output.moveCursor(0, 0);
		let maze = Maze.new(seed, size);

		let lastCellIndex = (maze.getRows() * maze.getCols()) - 1;

		// create player and start the game
		let player = Player.new(maze.getStart(), size, maze.getGrid());
		do player.draw();

		while (~(gameHasEnded)) {
			let move = Keyboard.keyPressed();
			if (move > 0) {
				do player.move(move);
				do Sys.wait(200);

				let gameHasEnded = Main.checkForEnd(player, maze, lastCellIndex);
			}
		}

		return;
	}

	/** Checks if the game has ended */
	function boolean checkForEnd(Player player, Maze maze, int lastCellIndex) {
		var int gridIndex;

		// players current position in the grid
		let gridIndex = (player.j() * maze.getCols()) + player.i();
		if (gridIndex = lastCellIndex) {
			do Output.printString("You completed the maze!");
			return true;
		}

		return false;
	}
}