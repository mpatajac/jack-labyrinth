class Cell {
	field int size, row, col, i, j;
	field boolean right, bottom, visited;

	/** Constructor function for the Cell object */
	constructor Cell new(int _row, int _col, int _size, Point start) {
		let size = _size;

		// position in the grid array
		let i = _row;
		let j = _col;

		// actual position on the screen
		let row = (i * size) + start.y();
		let col = (j * size) + start.x();

		// when first created, both the right and bottom walls exist
		let right = true;
		let bottom = true;

		// when first created, the cell isn't visited
		let visited = false;

		return this;
	}

	/** Method for drawing the cell onto the screen */
	method void draw() {
		// if exists, draw the right wall
		if (right) {
			do Screen.drawLine(
				col + size,
				row,
				col + size,
				row + size
			);
		}

		// if exists, draw the bottom wall
		if (bottom) {
			do Screen.drawLine(
				col + size,
				row + size,
				col,
				row + size
			);
		}

		return;
	}

	/** Setter method for the right wall */
	method void setRight(boolean b) {
		let right = b;
		return;
	}

	/** Setter method for the bottom wall */
	method void setBottom(boolean b) {
		let bottom = b;
		return;
	}

	/** Method for switching the "visited" field from false to true */
	method void visit() {
		let visited = true;
		return;
	}

	/** Getter method for field 'i' */
	method int i() {
		return i;
	}

	/** Getter method for field 'j' */
	method int j() {
		return j;
	}
}